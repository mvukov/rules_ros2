workspace(name = "com_github_mvukov_rules_ros2_examples")

# This import is relevant for these examples and this (rules_ros2) repository.
local_repository(
    name = "com_github_mvukov_rules_ros2",
    path = "..",
)

# In a normal workflow, you would typically import rules_ros2 into your
# (mono)repo as follows:
# http_archive(
#     name = "com_github_mvukov_rules_ros2",
#     # Here you can use e.g. sha256sum cli utility to compute the sha sum.
#     sha256 = "<sha sum of the .tar.gz archive below>",
#     strip_prefix = "rules_ros2-<git commit SHA>",
#     url = "https://github.com/mvukov/rules_ros2/archive/<git commit SHA>.tar.gz",
# )

load("@com_github_mvukov_rules_ros2//repositories:repositories.bzl", "ros2_repositories")

ros2_repositories()

load("@com_github_mvukov_rules_ros2//repositories:deps.bzl", "ros2_deps")

ros2_deps()

load("@rules_python//python:repositories.bzl", "py_repositories", "python_register_toolchains")

py_repositories()

python_register_toolchains(
    name = "rules_ros2_python",
    python_version = "3.10",
)

load("@com_github_mvukov_rules_ros2//repositories:pip_annotations.bzl", "PIP_ANNOTATIONS")
load("@rules_python//python:pip.bzl", "pip_parse")
load("@rules_ros2_python//:defs.bzl", python_interpreter_target = "interpreter")

pip_parse(
    name = "rules_ros2_pip_deps",
    annotations = PIP_ANNOTATIONS,
    python_interpreter_target = python_interpreter_target,
    requirements_lock = "@com_github_mvukov_rules_ros2//:requirements_lock.txt",
)

load(
    "@rules_ros2_pip_deps//:requirements.bzl",
    install_rules_ros2_pip_deps = "install_deps",
)

install_rules_ros2_pip_deps()

# Below are listed only deps needed by examples: if you just need ROS 2 you don't
# need to import/load anything below.

load("@bazel_tools//tools/build_defs/repo:http.bzl", "http_archive")

# rules_docker is not strictly necessary, but interesting if you want to create
# and/or push docker containers for the examples. The docker executable is
# needed only if you want to run an image using Bazel.
http_archive(
    name = "io_bazel_rules_docker",
    sha256 = "b1e80761a8a8243d03ebca8845e9cc1ba6c82ce7c5179ce2b295cd36f7e394bf",
    urls = ["https://github.com/bazelbuild/rules_docker/releases/download/v0.25.0/rules_docker-v0.25.0.tar.gz"],
)

load(
    "@io_bazel_rules_docker//repositories:repositories.bzl",
    container_repositories = "repositories",
)

container_repositories()

load("@io_bazel_rules_docker//repositories:deps.bzl", container_deps = "deps")

container_deps()

load(
    "@io_bazel_rules_docker//container:container.bzl",
    "container_pull",
)

# The base docker image is defined in [mvukov/bazel_deploy_base](https://github.com/mvukov/bazel_deploy_base) repo.
container_pull(
    name = "ros_deploy_base",
    digest = "sha256:322c3900f36c4078f4a7831026bd1782c859d27f0a8fc909c9928e59bdd764bb",
    registry = "docker.io",
    repository = "mvukov/bazel-deploy-base",
)
