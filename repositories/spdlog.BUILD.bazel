""" Builds spdlog.
"""

load("@bazel_skylib//lib:selects.bzl", "selects")
load("@com_github_mvukov_rules_ros2//ros2:cc_opts.bzl", "CPP_COPTS")
load("@rules_cc//cc:defs.bzl", "cc_library")

cc_library(
    name = "spdlog",
    srcs = glob(
        ["src/**/*.cpp"],
        exclude = ["src/fmt.cpp"],
    ) + ["include/spdlog/fmt/fmt.h"],
    hdrs = glob(
        ["include/**/*.h"],
        exclude = ["include/spdlog/fmt/bundled/*.h"],
    ),
    copts = CPP_COPTS + ["-w"],
    defines = [
        "SPDLOG_COMPILED_LIB",
        "SPDLOG_FMT_EXTERNAL",
    ],
    includes = ["include"],
    linkopts = selects.with_or(
        {
            ("@platforms//os:linux", "@platforms//os:macos"): ["-lpthread"],
            ("@platforms//os:qnx", "@platforms//os:android"): [],
        },
        no_match_error = "Only Linux, macOS , QNX and Android are supported!",
    ),
    visibility = ["//visibility:public"],
    deps = ["@fmt"],
)
