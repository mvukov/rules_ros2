""" Builds xacro.
"""

load("@rules_python//python:defs.bzl", "py_binary", "py_library")
load("@rules_ros2_pip_deps//:requirements.bzl", "requirement")

py_library(
    name = "xacro",
    srcs = glob(["xacro/*.py"]),
    visibility = ["//visibility:public"],
    deps = [
        "@ros2_ament_index//:ament_index_python",
        requirement("pyyaml"),
    ],
)

genrule(
    name = "main",
    outs = ["main.py"],
    cmd = """cat > $@ << EOL
import argparse
import contextlib
import sys

import xacro

parser = argparse.ArgumentParser()
parser.add_argument('--output', type=str, required=True)
args, xacro_args = parser.parse_known_args()

with open(args.output, 'w', encoding='utf-8') as stream:
    with contextlib.redirect_stdout(stream):
        sys.argv = sys.argv[:1] + xacro_args
        xacro.main()
EOL
""",
)

py_binary(
    name = "app",
    srcs = [":main"],
    main = "main.py",
    visibility = ["//visibility:public"],
    deps = [":xacro"],
)
